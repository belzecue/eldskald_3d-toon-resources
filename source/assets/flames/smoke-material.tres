[gd_resource type="ShaderMaterial" load_steps=4 format=2]

[sub_resource type="Shader" id=1]
code = "// Outline, by GDQuest
// Source: https://github.com/GDQuest/godot-shaders
shader_type spatial;
render_mode cull_front, unshaded, depth_draw_always;

uniform vec4 outline_color : hint_color = vec4(0.0, 0.0, 0.0, 1.0);
uniform float outline_width = 1.0;



void vertex() {
	// Converts vertex position and normal to clip coordinates.
	vec4 clip_position = PROJECTION_MATRIX * (MODELVIEW_MATRIX * vec4(VERTEX, 1.0));
	vec3 clip_normal = mat3(PROJECTION_MATRIX) * (mat3(MODELVIEW_MATRIX) * NORMAL);

	// Calculates the width based off of view distance to keep
	// the outline width constant.
	clip_position.xy += normalize(clip_normal.xy) / VIEWPORT_SIZE * clip_position.w * outline_width * 2.0;

	POSITION = clip_position;
}



void fragment() {
	ALBEDO = outline_color.rgb;
	if(outline_color.a < 1.0) {
		ALPHA = outline_color.a;
	}
}


"

[sub_resource type="ShaderMaterial" id=2]
resource_local_to_scene = true
shader = SubResource( 1 )
shader_param/outline_color = Color( 0, 0, 0, 1 )
shader_param/outline_width = 2.0

[sub_resource type="Shader" id=3]
code = "// Toon smoke shader.
// It's a simplified version of the refraction shader on fragment and light,
// with an added vertex function to morph its shape with time.
shader_type spatial;
render_mode depth_draw_always;

uniform float lighting : hint_range(0,1) = 0.0;
uniform float lighting_half_band : hint_range(0,1) = 0.25;
uniform float lighting_smoothness : hint_range(0,1) = 0.02;

uniform float rim : hint_range(0,1) = 0.05;
uniform float rim_amount: hint_range(0,1) = 0.2;
uniform float rim_smoothness: hint_range(0,1) = 0.05;

varying float alpha;



// We need this to pass the alpha value to the light pass.
void vertex() {
	alpha = COLOR.a;
}



void fragment() {
	// Reading from screen to ignore other particles.
	vec4 screen = texture(SCREEN_TEXTURE, SCREEN_UV);
	ALBEDO = mix(screen.rgb, COLOR.rgb, COLOR.a);
}



void light() {
	// Lighting part of the base toon shader. No specular reflection. If you want it,
	// just copy it directly from the base toon shader code.
	float shade = clamp(dot(NORMAL, LIGHT), 0.0, 1.0);
	float dark_shade = smoothstep(0.0, lighting_smoothness, shade);
	float half_shade = smoothstep(lighting_half_band, lighting_half_band + lighting_smoothness, shade);
	vec3 litness = (dark_shade/2.0 + half_shade/2.0) * ATTENUATION;
	vec3 diffuse = vec3(0.0);
	diffuse.r += ALBEDO.r * LIGHT_COLOR.r * mix(lighting, 1.0, litness.r);
	diffuse.g += ALBEDO.g * LIGHT_COLOR.g * mix(lighting, 1.0, litness.g);
	diffuse.b += ALBEDO.b * LIGHT_COLOR.b * mix(lighting, 1.0, litness.b);
	DIFFUSE_LIGHT += mix(ALBEDO, diffuse, alpha); // This last line is to add transparency.
	
	float rim_dot = 1.0 - dot(NORMAL, VIEW);
	float rim_threshold = pow((1.0 - rim_amount), shade);
	float rim_intensity = smoothstep(rim_threshold - rim_smoothness/2.0, rim_threshold + rim_smoothness/2.0, rim_dot);
	vec3 specular = vec3(0.0);
	specular += mix(vec3(0.0), LIGHT_COLOR, rim) * rim_intensity * litness;
	SPECULAR_LIGHT += mix(ALBEDO, specular, alpha) * rim_intensity * rim; // This last line is to add transparency.
}


"

[resource]
next_pass = SubResource( 2 )
shader = SubResource( 3 )
shader_param/lighting = 0.0
shader_param/lighting_half_band = 0.0
shader_param/lighting_smoothness = 0.02
shader_param/rim = 0.5
shader_param/rim_amount = 0.6
shader_param/rim_smoothness = 0.05
